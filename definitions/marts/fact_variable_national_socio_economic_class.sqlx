config {
    type: "table",
    columns: {
        constituencyid: "The hashed uniqueid for each Constituency",
        onsconstid: "The natural uniqueid for each Constituency",
        constituency_name: "The name of the Constituency",
            },
    assertions: {
        uniqueKey: ["constituencyid"]
    }
}

-- File name is fact_national_socio_economic_class.sqlx

WITH
  basePivot AS (
  SELECT
    constituencyid,
    onsconstid,
    constituency_name,
    variable,
    con_pc
  FROM
    ${ref("src_national_socio_economic_class")} )

SELECT
  *
FROM
  basePivot
PIVOT
  ( SUM(con_pc) FOR LOWER(variable) IN( 'higher_managerial_administrative_and_professional_occupations',
      'lower_managerial_administrative_and_professional_occupations',
      'small_employers_and_own_account_workers',
      'lower_supervisory_and_technical_occupations',
      'intermediate_occupations', 
      'semiroutine_occupations',
      'routine_occupations',
      'never_worked_longterm_unemployed',
      'fulltime_students') 
    )